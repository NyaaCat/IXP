apply plugin: "java-library"
apply plugin: "maven-publish"

ext.majorVersion = 1
ext.minorVersion = 0
ext.minecraftVersion = "1.14.1"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"

// Version used for distribution. Different from maven repo
group = "cn.eatmedicine"
archivesBaseName = "ItemXchgPortal-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber"

repositories {
    jcenter()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'sk89q';       url 'http://maven.sk89q.com/artifactory/repo/' }
    maven { name 'vault-repo';  url 'http://nexus.hc.to/content/repositories/pub_releases' }
    maven { name 'NyaaCat';     url 'https://ci.nyaacat.com/maven/' }
    maven { name 'EssentialsX'; url 'https://ci.ender.zone/plugin/repository/everything' }
}

dependencies {
    // spigot
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"

    // other nyaa plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
    } else {
        compileOnly('cat.nyaa:nyaacore:6.4-SNAPSHOT')
    }

    // 3rd party plugins
    compileOnly ('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly 'io.netty:netty-all:4.1.25.Final' // Spigot shadows this
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1' // Spigot shadows this
}

// publications
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
processResources { // modify version string
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            groupId "cn.eatmedicine"
            artifactId "ixp"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "${mavenDirectory}"
        }
    }
}
